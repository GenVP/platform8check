#Использовать v8runner
#Использовать tempfiles
#Использовать fs

// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd

Перем БДД; //контекст фреймворка 1bdd

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯСохраняюПутьКИсходнымКодамТестовойКонфигурацииВПеременную");
	ВсеШаги.Добавить("ЯСохраняюРезультатТестовойПроверкиВПеременную");
	ВсеШаги.Добавить("ЯСохраняюТестовыйФильтрФайловИсходныхКодовВПеременную");
	ВсеШаги.Добавить("ЯСоздаюВременнуюБазуИСохраняюВПеременную");
	ВсеШаги.Добавить("ЯЗагружаюКонфигурациюИзФайловВКаталогеВБазу");
	ВсеШаги.Добавить("ЯСоздаюОбъектУправлениеКонфигураторомДляБазыИСохраняюВПеременную");
	ВсеШаги.Добавить("ЯСоздаюМенеджерПроверок");
	ВсеШаги.Добавить("ЯУстанавливаюДляПроверокКонтекстБазыИсточника");
	ВсеШаги.Добавить("ЯУстанавливаюДляПроверокКонфигураторБазыИсточника");
	ВсеШаги.Добавить("ЯУстанавливаюДляПроверокКаталогИсходныхКодов");
	ВсеШаги.Добавить("ЯСоздаюБазуДляПроверокИзФайловИсходныхКодов");
	ВсеШаги.Добавить("ЯУстанавливаюДляПроверокВремяОжидания");
	ВсеШаги.Добавить("ЯУстанавливаюДляПроверокМаксимальноеВремяОжидания");
	ВсеШаги.Добавить("ЯДобавляюФильтрФайлов");
	ВсеШаги.Добавить("ЯНачинаюПроверкуСКлючами");
	ВсеШаги.Добавить("ЯДобавляюРезультатПроверкиСКлючами");
	ВсеШаги.Добавить("ЯсохраняюРезультатЗавершенныхПроверокВПеременную");
	ВсеШаги.Добавить("КоличествоСтрокВТаблицеРавно");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	
КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт

	ВременныеФайлы.Удалить();

КонецПроцедуры

//Я сохраняю путь к исходным кодам тестовой конфигурации в переменную "КаталогИсходников"
Процедура ЯСохраняюПутьКИсходнымКодамТестовойКонфигурацииВПеременную(ИмяПеременнойИсходников) Экспорт

	ПутьККаталогу = ВременныеФайлы.СоздатьКаталог();
	ПутьККаталогуЭталону = ОбъединитьПути(БДД.КаталогПроверяемогоПроекта(), "tests\fixtures\testbase\src");
	ФС.КопироватьСодержимоеКаталога(ПутьККаталогуЭталону, ПутьККаталогу);
	БДД.СохранитьВКонтекст(ИмяПеременнойИсходников, ПутьККаталогу);

КонецПроцедуры

//Я сохраняю результат тестовой проверки в переменную "РезультатПроверки"
Процедура ЯСохраняюРезультатТестовойПроверкиВПеременную(ИмяПеременнойРезультатаПроверки) Экспорт

	ПутьКРезультату = ВременныеФайлы.НовоеИмяФайла("log");
	ПутьКРезультатуЭталону = ОбъединитьПути(БДД.КаталогПроверяемогоПроекта(), "tests\fixtures\testbase\result.log");
	КопироватьФайл(ПутьКРезультатуЭталону, ПутьКРезультату);
	БДД.СохранитьВКонтекст(ИмяПеременнойРезультатаПроверки, ПутьКРезультату);

КонецПроцедуры

//Я сохраняю тестовый фильтр файлов исходных кодов в переменную "ФильтрФайлов"
Процедура ЯСохраняюТестовыйФильтрФайловИсходныхКодовВПеременную(Знач ИмяПеременойФильтра) Экспорт

	ПутьКФильтру = ВременныеФайлы.НовоеИмяФайла("txt");
	ПутьКФильтруЭталону = ОбъединитьПути(БДД.КаталогПроверяемогоПроекта(), "tests\fixtures\testbase\src_files.txt");
	КопироватьФайл(ПутьКФильтруЭталону, ПутьКФильтру);
	БДД.СохранитьВКонтекст(ИмяПеременойФильтра, ПутьКФильтру);

КонецПроцедуры

//Я создаю временную базу и сохраняю в переменную "ВременнаяБаза"
Процедура ЯСоздаюВременнуюБазуИСохраняюВПеременную(Знач ИмяПеременнойБазы) Экспорт

	ПутьКВременнойБазе = ВременныеФайлы.СоздатьКаталог();

	УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
	УправлениеКонфигуратором.СоздатьФайловуюБазу(ПутьКВременнойБазе);

	БДД.СохранитьВКонтекст(ИмяПеременнойБазы, ПутьКВременнойБазе);

КонецПроцедуры

//И Я создаю объект УправлениеКонфигуратором для базы "КаталогБазы" и сохраняю в переменную "КонтекстБазы"
Процедура ЯСоздаюОбъектУправлениеКонфигураторомДляБазыИСохраняюВПеременную(Знач КаталогБазы, Знач ИмяПеременнойКонфигуратора) Экспорт

	УправлениеКонфигуратором = Новый УправлениеКонфигуратором();
	УправлениеКонфигуратором.УстановитьКонтекст(СтрШаблон("/F""%1""", ЗаменитьПеременнуюВЗначении(КаталогБазы)), "", "");

	БДД.СохранитьВКонтекст(ИмяПеременнойКонфигуратора, УправлениеКонфигуратором);

КонецПроцедуры

//Я загружаю конфигурацию из файлов в каталоге "КаталогИсходников" в базу "КаталогБазы"
Процедура ЯЗагружаюКонфигурациюИзФайловВКаталогеВБазу(КаталогИсходников, КаталогБазы) Экспорт

	УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
	УправлениеКонфигуратором.УстановитьКонтекст(СтрШаблон("/F%1", ЗаменитьПеременнуюВЗначении(КаталогБазы)), "", "");
	УправлениеКонфигуратором.ЗагрузитьКонфигурациюИзФайлов(ЗаменитьПеременнуюВЗначении(КаталогИсходников), "", "", Ложь);

КонецПроцедуры

//Я создаю менеджер проверок
Процедура ЯСоздаюМенеджерПроверок() Экспорт

	МенеджерПроверок = Новый МенеджерПлатформенныхПроверок();
	БДД.СохранитьВКонтекст("МенеджерПроверок", МенеджерПроверок);

КонецПроцедуры

//Я устанавливаю для проверок контекст базы источника "КаталогБазы" 
Процедура ЯУстанавливаюДляПроверокКонтекстБазыИсточника(КаталогБазы) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.КонтекстБазыИсточника(ЗаменитьПеременнуюВЗначении(КаталогБазы), "", "");

КонецПроцедуры

//Я устанавливаю для проверок конфигуратор базы источника "КонфигураторБазы"
Процедура ЯУстанавливаюДляПроверокКонфигураторБазыИсточника(КонфигураторБазы) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.КонфигураторБазыИсточника(ЗаменитьПеременнуюВЗначении(КонфигураторБазы));

КонецПроцедуры

//Я устанавливаю для проверок каталог исходных кодов "КаталогИсходников" 
Процедура ЯУстанавливаюДляПроверокКаталогИсходныхКодов(КаталогИсходников) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.КаталогИсходныхКодов(ЗаменитьПеременнуюВЗначении(КаталогИсходников));

КонецПроцедуры

//Я устанавливаю для проверок время ожидания 60
Процедура ЯУстанавливаюДляПроверокВремяОжидания(ВремяОжидания) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.ВремяОжидания(ЗаменитьПеременнуюВЗначении(ВремяОжидания));

КонецПроцедуры

//Я устанавливаю для проверок максимальное время ожидания 5400
Процедура ЯУстанавливаюДляПроверокМаксимальноеВремяОжидания(МаксимальноеВремяОжидания) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.МаксимальноеВремяОжидания(ЗаменитьПеременнуюВЗначении(МаксимальноеВремяОжидания));

КонецПроцедуры

//Я добавляю фильтр файлов "ФильтрФайлов"
Процедура ЯДобавляюФильтрФайлов(ФильтрФайлов) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	ПутьКФайлуФильтра = ЗаменитьПеременнуюВЗначении(ФильтрФайлов);
	МенеджерПроверок.ДобавитьФильтр(Новый ФильтрФайловПоСписку(ПутьКФайлуФильтра));

КонецПроцедуры 

//Я начинаю проверку с ключами "КлючиПроверки"
Процедура ЯНачинаюПроверкуСКлючами(КлючиПроверки) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.НачатьПроверку(ЗаменитьПеременнуюВЗначении(КлючиПроверки));

КонецПроцедуры

//Я Добавляю результат проверки "РезультатПроверки" с ключами "КлючиПроверки" 
Процедура ЯДобавляюРезультатПроверкиСКлючами(РезультатПроверки, КлючиПроверки) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	ФайлРезультатаПроверки = ЗаменитьПеременнуюВЗначении(РезультатПроверки);
	СтрокаКлючейПроверки = ЗаменитьПеременнуюВЗначении(КлючиПроверки);
	МенеджерПроверок.ДобавитьРезультатПроверки(КлючиПроверки, ФайлРезультатаПроверки);

КонецПроцедуры

//Я сохраняю результат завершенных проверок в переменную "ТаблицуОшибок"
Процедура ЯсохраняюРезультатЗавершенныхПроверокВПеременную(ИмяПеременнойОшибок) Экспорт

	МенеджерПроверок = БДД.ПолучитьИзКонтекста("МенеджерПроверок");
	МенеджерПроверок.ЗавершитьВсеПроверки();

	ТаблицаОшибок = МенеджерПроверок.ТаблицаОшибок();
	БДД.СохранитьВКонтекст(ИмяПеременнойОшибок, ТаблицаОшибок);

КонецПроцедуры

//Количество строк в таблице "ТаблицаОшибок" равно 17
Процедура КоличествоСтрокВТаблицеРавно(ТаблицаДанных, ОжидаемоеКоличество) Экспорт

	ТаблицаОшибок = ЗаменитьПеременнуюВЗначении(ТаблицаДанных);

	Ожидаем.Что(ТаблицаОшибок).ИмеетДлину(ОжидаемоеКоличество);

КонецПроцедуры

Функция ЗаменитьПеременнуюВЗначении(Знач Значение) Экспорт

	Если ТипЗнч(Значение) = Тип("Строка") И СтрНачинаетсяС(Значение, "$") И СтрЗаканчиваетсяНа(Значение, "$") Тогда
		Результат = БДД.ЗначениеСУчетомПеременныхКонтекста(Сред(Значение, 2, СтрДлина(Значение) - 2));
	Иначе
		Результат = Значение;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции